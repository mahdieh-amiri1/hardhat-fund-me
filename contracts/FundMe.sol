// SPDX-License-Identifier: MIT
// 1. Pragma
pragma solidity ^0.8.7;
// 2. Imports
import "@chainlink/contracts/src/v0.8/interfaces/AggregatorV3Interface.sol";
import "./PriceConverter.sol";

// 3. Interfaces, Libraries, Contracts
error FundMe__NotOwner();

/**@title A sample Funding Contract
 * @author Patrick Collins
 * @notice This contract is for creating a sample funding contract
 * @dev This implements price feeds as our library
 */
contract FundMe {
    // Type Declarations
    using PriceConverter for uint256;

    // State variables
    uint256 public constant MINIMUM_USD = 50 * 10**18;
    address private immutable i_owner;
    address[] private s_funders;
    mapping(address => uint256) private s_addressToAmountFunded;
    AggregatorV3Interface private s_priceFeed;

    // Events
    event FundEvent(address fundingAddress, uint256 fundingAmount);
    event RefundEvent(address refundingAddress, uint256 refundingAmount);
    event WithdrawEvent(address withdrawAddreess, uint256 withdrawAmount);

    // Modifiers
    modifier onlyOwner() {
        // require(msg.sender == i_owner);
        if (msg.sender != i_owner) revert FundMe__NotOwner();
        _;
    }

    // Functions Order:
    //// constructor
    //// receive
    //// fallback
    //// external
    //// public
    //// internal
    //// private
    //// view / pure

    constructor(address priceFeed) {
        s_priceFeed = AggregatorV3Interface(priceFeed);
        i_owner = msg.sender;
    }

    /// @notice Funds our contract based on the ETH/USD price
    function fund() public payable {

        require(
            msg.value.getConversionRate(s_priceFeed) >= MINIMUM_USD,
            "You need to spend more ETH!"
        );
        s_addressToAmountFunded[msg.sender] += msg.value;
        s_funders.push(msg.sender);
        emit FundEvent(msg.sender, msg.value);
    }

    function refund(address payable refundingAddress) public onlyOwner {
        uint256 refundingAmount = s_addressToAmountFunded[refundingAddress];
        require(refundingAmount > 0, "There is no amount to refund!");

        s_addressToAmountFunded[refundingAddress] = 0;
        refundingAmount -= gasleft() * 2;
        (bool success, ) = refundingAddress.call{value: refundingAmount}("");
        require(success);
        emit RefundEvent(refundingAddress, refundingAmount);
    }


    function withdraw() public onlyOwner {
        require(address(this).balance != 0, "No balance to withdraw");
        for (
            uint256 funderIndex = 0;
            funderIndex < s_funders.length;
            funderIndex++
        ) {
            address funder = s_funders[funderIndex];
            s_addressToAmountFunded[funder] = 0;
        }
        s_funders = new address[](0);
        uint256 contractBalance = address(this).balance;
        // payable(msg.sender).transfer(address(this).balance);
        (bool success, ) = i_owner.call{value: contractBalance}("");
        require(success);
        emit WithdrawEvent(msg.sender, contractBalance);
    }

    function cheaperWithdraw() public onlyOwner {
        address[] memory funders = s_funders;
        // mappings can't be in memory, sorry!
        for (
            uint256 funderIndex = 0;
            funderIndex < funders.length;
            funderIndex++
        ) {
            address funder = funders[funderIndex];
            s_addressToAmountFunded[funder] = 0;
        }
        s_funders = new address[](0);

        uint256 contractBalance = address(this).balance;

        // payable(msg.sender).transfer(address(this).balance);
        (bool success, ) = i_owner.call{value: contractBalance}("");
        require(success);
        emit WithdrawEvent(msg.sender, contractBalance);
    }

    /** @notice Gets the amount that an address has funded
     *  @param fundingAddress the address of the funder
     *  @return the amount funded
     */
    function getAddressToAmountFunded(address fundingAddress)
        public
        view
        returns (uint256)
    {
        return s_addressToAmountFunded[fundingAddress];
    }

    function getVersion() public view returns (uint256) {
        return s_priceFeed.version();
    }

    function getFunder(uint256 index) public view returns (address) {
        return s_funders[index];
    }

    function getOwner() public view returns (address) {
        return i_owner;
    }

    function getPriceFeed() public view returns (AggregatorV3Interface) {
        return s_priceFeed;
    }
}
